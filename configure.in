dnl Process this file with autoconf to produce a configure script.
dnl $Id: configure.in,v 1.67 2012/08/15 21:27:07 jjg Exp $

AC_INIT(cptutils, 1.46, j.j.green@gmx.fr)

CFXTRA="-Wall"

dnl Checks for programs.

AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL

AC_PATH_PROG(PYTHON,python,no)
if test "$PYTHON" = no; then
  AC_MSG_WARN(no python! (no convert-gradient for you))       
fi

AC_PATH_PROG(GGO,gengetopt,no)
if test "$GGO" = no; then
  AC_MSG_WARN(no gengetopt! (not needed for compile))       
fi

AC_PATH_PROG(GPERF,gperf,no)
if test "$GPERF" = no; then
  AC_MSG_WARN(no gperf! (not needed for compile))       
fi

AC_PATH_PROG(XML2C,xml2-config,no)
if test "$XML2C" = no
then
  AC_MSG_ERROR(no xml2-config! please install libxml2) 
else
  CFLAGS="$CFLAGS `$XML2C --cflags`"
  LIBS="$LIBS `$XML2C --libs`"
fi

AC_PATH_PROG(XSLTP,xsltproc,no)
if test "$XSLTP" = no; then
  AC_MSG_WARN(no xsltproc! (not needed for compile))       
fi

if test "$GCC"x = yesx; then
  CFLAGS="$CFLAGS $CFXTRA"
fi

dnl Checks for libraries.

AC_CHECK_LIB(m,pow)
AC_CHECK_LIB(png,png_create_read_struct,[],AC_MSG_ERROR(libpng not found))
AC_SEARCH_LIBS(iconv,iconv,[],AC_MSG_ERROR(iconv not found))

dnl Checks for header files.

AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(glob.h)
AC_CHECK_HEADERS(search.h)
AC_CHECK_HEADERS(iconv.h)
AC_CHECK_HEADERS(png.h)

dnl One of these may have htons

AC_CHECK_HEADERS(arpa/inet.h)
AC_CHECK_HEADERS(netinet/in.h)

dnl Checks for library functions.

AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(getopt)
AC_CHECK_FUNCS(glob)
AC_CHECK_FUNCS(tsearch)
AC_CHECK_FUNCS(htons)
AC_CHECK_FUNCS(ntohs)

dnl used by gengetop output

AC_DEFINE(VERSION,PACKAGE_VERSION,"version of package")

dnl used in Common.mk 

AC_SUBST(PACKAGE_VERSION)

dnl Make a config.h

AC_CONFIG_HEADER(src/common/config.h)

dnl Create Makefile data

AC_OUTPUT([Makefile src/Common.mk])

